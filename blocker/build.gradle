plugins {
    id "java"
    id "org.springframework.boot"
    id "io.spring.dependency-management"
    id "org.springframework.cloud.contract"
    id "maven-publish"
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    contractTestImplementation 'org.springframework.cloud:spring-cloud-starter-contract-verifier'
    testImplementation 'io.rest-assured:spring-web-test-client'
}

contracts {
    contractsDslDir = file("src/contractTest/resources/contracts")
    testMode = 'WEBTESTCLIENT'
    baseClassForTests = "by.tyv.blocker.BaseContractTest"
}

publishing {
    publications {
        create("mavenJava", MavenPublication) {
            from components.java
            // прикрепляем stubs-jar, у задачи уже есть classifier 'stubs'
            artifact tasks.named("verifierStubsJar")
        }
    }
}

tasks.named("check") { dependsOn("contractTest") }
tasks.named("verifierStubsJar") { dependsOn("contractTest") }
tasks.named("publishToMavenLocal") { dependsOn("verifierStubsJar") }
